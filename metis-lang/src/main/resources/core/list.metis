fn list.__str__(self)
    let sb = string.builder()
    sb.append("[")
    for v in self
        if v is self
            sb.append("[...]")
        else
            sb.append(v)
        end
        sb.append(", ")
    end
    if self.size() > 1
        sb.deleteLast(2)
    end
    return str(sb.append("]"))
end

fn list.extend(self, other)
    for v in other
        self.append(v)
    end
end

fn list.flatten(self)
    let result = list.new()
    for v in self
        if type(v) == "list"
            result.extend(v.flatten())
        else
            result.append(v)
        end
    end
    return result
end

fn list.join(self, sep)
    let sb = string.builder()
    for v in self
        sb.append(v).append(sep)
    end
    sb.deleteLast(sep.size())
    return str(sb)
end

fn list.pop(self) = self.removeAt(self.size() - 1)

fn list.reversed(self)
    let result = list.new(self.size())
    for i in range.inclusive(self.size() - 1, 0, -1)
        result.append(self[i])
    end
    return result
end

fn list.sort(self, compare)
    compare ?:= cmp
    let pivot = self[self.size() // 2]
    let less = list.new()
    let equal = list.new()
    let greater = list.new()
    for v in self
        let cmp = compare(v, pivot)
        if cmp < 0
            less.append(v)
        elif cmp > 0
            greater.append(v)
        else
            equal.append(v)
        end
    end
    if less.size() > 1
        less.sort(compare)
    end
    if greater.size() > 1
        greater.sort(compare)
    end
    self.clear()
    self.extend(less)
    self.extend(equal)
    self.extend(greater)
end

fn list.sorted(self, compare)
    let result = self.clone()
    result.sort(compare)
    return result
end

fn list.clone(self)
    let result = list.new(self.size())
    for v in self
        result.append(v)
    end
    return result
end

fn list.__plus__(self, value)
    let new = self.clone()
    if type(value) == "list"
        for v in value
            new.append(v)
        end
    else
        new.append(value)
    end
    return new
end

list.metatable = collection
bytes.metatable = collection