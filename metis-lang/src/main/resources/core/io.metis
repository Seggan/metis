let BUFSIZE = 2048
fn io.inStream.readAll(self)
    let result = []
    let buffer = bytes.allocate(BUFSIZE)
    let read = self.read(buffer)
    while read is not null and read > 0
        result.append(buffer.slice(0, read))
        read = self.read(buffer)
    end
    return bytes.concat(result)
end

fn io.inStream.readText(self)
    let result = ""
    do
        result = self.readAll().decode()
    finally
        self.close()
    end
    return result
end

fn io.inStream.readLine(self)
    let result = string.builder()
    let c = self.read()
    while c is not null and c != 10
        result.append(c.char())
        c = self.read()
    end
    return str(result)
end

fn io.inStream.copyTo(self, out)
    let buffer = bytes.allocate(BUFSIZE)
    let read = self.read(buffer)
    while read is not null and read > 0
        out.write(buffer, 0, read)
        read = self.read(buffer)
    end
end

fn io.outStream.writeLine(self, line)
    self.write(line.encode())
    self.write('\n')
end

fn io.outStream.writeText(self, text)
    self.write(text.encode())
end

fn io.inStream.use(self, f)
    let result = null
    do
        result = f(self)
    finally
        self.close()
    end
    return result
end

fn io.outStream.use(self, f)
    let result = null
    do
        result = f(self)
    finally
        self.close()
    end
    return result
end

